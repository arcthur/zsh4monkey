#!/usr/bin/env zsh

emulate -L zsh -o typeset_silent -o pipe_fail -o extended_glob \
    -o prompt_percent -o no_prompt_subst -o no_prompt_bang -o no_bg_nice -o no_aliases
-z4m-check-core-params || return

if (( ARGC )); then
  print -Pru2 -- '%F{3}z4m%f: unexpected %F{1}update%f argument'
  return '_z4m_err()'
fi

if (( _z4m_dangerous_root )); then
  print -Pru2 -- "%F{3}z4m%f: refusing to %Bupdate%b as %F{1}root%f"
  return 1
fi

local old=$Z4M.old.$$
local new=$Z4M.new.$$

{
  zf_rm -rf -- $old $new   || return
  zf_mkdir -p -- $new      || return
  print -n >$new/.updating || return

  Z4M_UPDATING=$Z4M Z4M=$new </dev/null >/dev/null $_z4m_exe -ic '
    (( $? )) && "exit" "1"
    "builtin" "emulate" "zsh" "-o" "no_aliases"
    [[ $Z4M == '${(q)new}' ]] || exit 0
    print -n >$Z4M/tmp/update-successful' || return

  if [[ ! -e $new/tmp/update-successful ]]; then
    local home=~
    local zdotdir=${${${(q)ZDOTDIR}/#${(q)home}/'~'}//\%/%%}
    local z4m=${${${(q)Z4M}/#${(q)home}/'~'}//\%/%%}
    print -Pru2 -- '%F{3}z4m%f: %B$Z4M%b %F{1}does not propagate%f through %U.zshrc%u'
    print -Pru2 -- ''
    print -Pru2 -- 'Change %U'$zdotdir'/.zshrc%u to keep %BZ4M%b intact if already set.'
    print -Pru2 -- ''
    print -Pru2 -- 'For example:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{2}:%f %F{3}"${Z4M:=${XDG_CACHE_HOME:-$HOME/.cache}/zsh4monkey}"%f'
    print -Pru2 -- ''
    print -Pru2 -- 'Note: The leading colon (%F{2}:%f) is necessary.'
    return 1
  fi

  zf_rm -- $new/tmp/update-successful                || return
  zf_rm -- $new/.updating                            || return
  -z4m-mv $Z4M $old 2>/dev/null || zf_rm -rf -- $Z4M || return
  if [[ -e $Z4M ]]; then
    local home=~
    local z4m=${${${(q)Z4M}/#${(q)home}/'~'}//\%/%%}
    print -Pru2 -- '%F{3}z4m%f: %F{1}cannot delete %U'$z4m'%u%f'
    print -Pru2 -- ''
    print -Pru2 -- 'This might help:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{2}%Ucommand%u %Usudo%u rm%f -rf -- %U'$z4m'%u && %U%F{2}exec%u zsh%f'
    print -Pru2 -- ''
    print -Pru2 -- '%F{3}z4m%f: attempting to recover'
    if [[ ! -e $Z4M/stickycache ]]; then
      command cp -r -- $new/stickycache $Z4M/stickycache || true
    fi
    if [[ -o zle ]]; then
      exec -- $_z4m_exe -i || return
    else
      exec -- $_z4m_exe -i --no-zle || return
    fi
  fi
  -z4m-mv $new $Z4M || return
} always {
  if (( $? )); then
    -z4m-error-command update
  fi
  zf_rm -rf -- $old $new || return
}

print -Pru2 -- "%F{3}z4m%f: %Bupdate successful%b"
print -Pru2 -- "%F{3}z4m%f: restarting %F{2}zsh%f"
if [[ -o zle ]]; then
  exec -- $_z4m_exe -i || return
else
  exec -- $_z4m_exe -i --no-zle || return
fi
