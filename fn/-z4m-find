#!/usr/bin/env zsh

local dot_glob=$1
local only_dirs=$2
local dirs=("${@:3}")

(( $#dirs )) || return 0

# File pattern filters (from _path_files -g and -F options)
local -a file_pats=("${_z4m_find_pats[@]}")
local -a file_ignore=("${_z4m_find_ignore[@]}")

local -a bin
local -a flags
() {
  emulate -L zsh
  # Set dot_glob in case the value of find-flags depends on it (via `zstyle -e`).
  # Ideally we should run this with user options.
  (( dot_glob )) && setopt dot_glob
  local widget=${WIDGET#z4m-}
  zstyle -a :z4m:$widget find-command bin
  if (( ! $#bin )); then
    if (( $+commands[bfs] )); then
      bin=(command bfs)
    else
      bin=(command find)
    fi
  fi
  zstyle -a :z4m:$widget find-flags flags
  if (( ! $#flags )); then
    flags=(-name '.*' -prune -print -o -print)
  fi
}

local -a cmd
local -aU fss
fss=(${(f)"$("${bin[@]}" / . -maxdepth 0 -printf '%F\n' 2>/dev/null)"}) || fss=()
if (( $#fss )) && [[ -z ${(M)fss:#unknown} ]]; then
  cmd+=("${bin[@]}" -L ./$^dirs)
  (( only_dirs )) && cmd+=('!' -type d -prune -o)
  cmd+=('!' '(')
  local fs
  for fs in $fss; do
    cmd+=(-fstype $fs -o)
  done
  cmd[-1]=(')' -prune '(' "${flags[@]}" ')')
  (( dot_glob )) || cmd+=(-o -name '.*' -prune)
  cmd+=(-o "${flags[@]}")
else
  cmd+=("${bin[@]}" -L . -xdev -mindepth 1)
  (( only_dirs )) && cmd+=('!' -type d -prune -o)
  cmd+=('!' -path './*/*' '!' '(')
  local dir
  for dir in $dirs; do
    cmd+=(-name ${(b)dir} -o)
  done
  cmd[-1]=(')' -prune)
  (( dot_glob )) || cmd+=(-o -name '.*' -prune)
  cmd+=(-o "${flags[@]}")
fi

# Execute find and optionally filter results
if (( $#file_pats || $#file_ignore )); then
  # Filter output through zsh to respect glob patterns
  "${cmd[@]}" | while IFS= read -r line; do
    local name=${line:t}
    local -i match=1
    # Check if file matches any of the include patterns
    if (( $#file_pats )); then
      match=0
      local pat
      for pat in "${file_pats[@]}"; do
        if [[ $name == ${~pat} ]]; then
          match=1
          break
        fi
      done
    fi
    # Check if file matches any ignore pattern
    if (( match && $#file_ignore )); then
      local ign
      for ign in "${file_ignore[@]}"; do
        if [[ $name == ${~ign} ]]; then
          match=0
          break
        fi
      done
    fi
    (( match )) && print -r -- "$line"
  done
else
  "${cmd[@]}"
fi
