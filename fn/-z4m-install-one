#!/usr/bin/env zsh

local url postinstall command

if [[ $1 == */* ]]; then
  if [[ $1 == zsh-users/zsh-autosuggestions ]]; then
    print -Pru2 -- "%F{3}z4m%f: %Binstall%b: %B$1%b is built-in; remove this install entry"
    return 1
  fi
  if [[ $1 == *@* ]]; then
    url=https://github.com/${1%%@*}/archive/${1#*@}.tar.gz
    1=${1%%@*}
  else
    url=https://github.com/$1/archive/master.tar.gz
  fi
  if ! zstyle -s :z4m:$1 postinstall postinstall; then
    case $1 in
      ohmyzsh/ohmyzsh)
        postinstall=-z4m-postinstall-ohmyzsh
      ;;
      MichaelAquilina/zsh-you-should-use)
        postinstall=-z4m-postinstall-zsh-you-should-use
      ;;
      wfxr/forgit)
        postinstall=-z4m-postinstall-forgit
      ;;
      *)
        postinstall=
      ;;
    esac
  fi
else
  local -a channel
  zstyle -a :z4m:$1 channel channel || channel=(stable)
  case $#channel-$channel[1] in
    2-command) command=$channel[2];;
    1-stable|1-testing|1-dev)
      case $1 in
        terminfo)                     url=https://github.com/romkatv/terminfo/archive/v1.4.0.tar.gz;;
        tmux|fzf|eza|bat|fd|rg|zoxide|carapace|atuin)
          # CLI tools: postinstall checks system command
          url=
        ;;
        powerlevel10k)                url=https://github.com/romkatv/$1/archive/master.tar.gz;;
        zsh-completions)              url=https://github.com/zsh-users/$1/archive/master.tar.gz;;
        zsh-autosuggestions)
          print -Pru2 -- "%F{3}z4m%f: %Binstall%b: %B$1%b is built-in; remove this install entry"
          return 1
        ;;
        *)
          print -Pru2 -- "%F{3}z4m%f: %F{1}internal error%f: unknown package: ${1//\%/%%}"
          return 1
        ;;
      esac
    ;;
    1-none)
      zf_mkdir -p -- $Z4M/$1 || return
      return
    ;;
    *)
      print -Pru2 -- '%F{3}z4m%f: invalid zstyle'
      print -Pru2 -- ''
      print -Pru2 -- "  %F{2}zstyle%f :z4m:${(q)1//\%/%%} channel %F{1}${(j: :)${(@q)channel//\%/%%}}%f"
      print -Pru2 -- ''
      print -Pru2 -- 'Supported values: %Bstable%b, %Btesting%b and %Bdev%b.'
      return 1
    ;;
  esac
  (( $+functions[-z4m-postinstall-$1] )) && postinstall=-z4m-postinstall-$1
fi

if [[ -e $Z4M/.updating ]]; then
  print -Pru2 -- "%F{3}z4m%f: updating %B${1//\%/%%}%b"
else
  print -Pru2 -- "%F{3}z4m%f: installing %B${1//\%/%%}%b"
fi

local dst=$Z4M/$1

zf_mkdir -p -- ${dst:h} 2>/dev/null || zf_mkdir -p -- ${dst:h} || return

local old new

{
  if (( $+commands[mktemp] )); then
    old="$(command mktemp -d -- $dst.old.XXXXXXXXXX)" || return
    new="$(command mktemp -d -- $dst.new.XXXXXXXXXX)" || return
  else
    old=$dst.old.$$
    new=$dst.new.$$
    zf_rm -rf -- $old $new   || return
    zf_mkdir -p -- $old $new || return
  fi

  local Z4M_PACKAGE_NAME=$1 Z4M_PACKAGE_DIR=$new/${1:t}

  if [[ -n $command ]]; then
    () { eval $command } || return 1
    if [[ ! -d $Z4M_PACKAGE_DIR ]]; then
      print -Pru2 -- "%F{3}z4m%f: custom command failed to install: %F{1}${Z4M_PACKAGE_DIR//\%/%%}%f"
      return 1
    fi
  elif [[ -z $url ]]; then
    # CLI tools: create directory, postinstall checks system and suggests install
    zf_mkdir -p -- $Z4M_PACKAGE_DIR || return
  else
    local err
    if (( $+commands[curl] )); then
      err="$(command curl -fsSL -- $url 2>&1 >$new/snapshot.tar.gz)"
    elif (( $+commands[wget] )); then
      err="$(command wget -O-   -- $url 2>&1 >$new/snapshot.tar.gz)"
    else
      print -Pru2 -- "%F{3}z4m%f: please install %F{1}curl%f or %F{1}wget%f"
      return 1
    fi
    if (( $? )); then
      print -ru2 -- $err
      print -Pru2 -- "%F{3}z4m%f: failed to download %F{1}${url//\%/%%}%f"
      return 1
    fi
    command tar -C $new -xzf $new/snapshot.tar.gz || return
    local dirs=($new/*-*(N/))
    if (( $#dirs != 1 )); then
      print -Pru2 -- "%F{3}z4m%f: invalid content: %F{1}${url//\%/%%}%f"
      return 1
    fi
    if [[ $dirs[1] != $new/${1:t} ]]; then
      -z4m-mv $dirs[1] $new/${1:t} || return
    fi
  fi

  eval $postinstall || return

  [[ ! -e $dst ]] || -z4m-mv $dst $old/${1:t} 2>/dev/null || zf_rm -rf -- $dst || return
  -z4m-mv $new/${1:t} $dst || return
} always {
  [[ -z $old && -z $new ]] || zf_rm -rf -- $old $new
}
