#!/usr/bin/env zsh

emulate -L zsh -o typeset_silent -o pipe_fail -o extended_glob \
    -o prompt_percent -o no_prompt_subst -o no_prompt_bang -o no_bg_nice -o no_aliases
setopt octalzeroes

zmodload zsh/system 2>/dev/null || return 1

local -i list_colors=$1
local -i list_types=$2
local -i tilde_unexpand=$3
local -i encode_key=${4:-0}

autoload +X -Uz -- -z4m-b64enc

[[ $_z4m_mode_colors[ln] != target ]]
local -i ln_target=$?

local -r nil=$'\0'
local -r or=$_z4m_mode_colors[or]
local -r ln=$_z4m_mode_colors[ln]
local -r suf=('|' '|' '' '' '' '' '/' '/' '' '' '' '' '' '' '' '*' '' '' '@' '@' '' '' '=' '=')

local -i10 m i
local buf file style
local -a files modes link link_mode
local -i MATCH
local MBEGIN MEND

while true; do
  while [[ $buf != *$'\n'* ]]; do
    sysread -s 4096 'buf[$#buf+1]' && continue
    (( $? == 5 ))                  || return
    return
  done

  files=("${(@f)buf}")
  buf=$files[-1]
  files[-1]=()

  if (( ! list_colors && ! list_types && ! tilde_unexpand )) || ! zstat -L -A modes +mode -- $files; then
    if (( encode_key )); then
      for file in $files; do
        local -a reply=()
        -z4m-b64enc "$file" || continue
        printf '%s\0%q\n' "$reply[1]" "$file"
      done
    else
      printf '%1$s\0%1$q\n' $files
    fi
    continue
  fi

  if (( ! list_colors && ! tilde_unexpand )); then
    if (( encode_key )); then
      for file m in ${files:^modes}; do
        local k=${file}${suf[$((((m & 61440) >> 11) | ! (m & 73)))]}
        local -a reply=()
        -z4m-b64enc "$k" || continue
        printf '%s\0%q\n' "$reply[1]" "$k"
      done
    else
      printf '%2$s\0%2$q%1$s\n' \
        ${"${(@)modes/(#m)*/$suf[$((((MATCH & 61440) >> 11) | !(MATCH & 73)))]}":^files}
    fi
    continue
  fi

  # Note: Color priority differs slightly from GNU ls's get_color_indicator in
  # https://git.savannah.gnu.org/cgit/coreutils.git/tree/src/ls.c.
  # GNU ls uses strict priority (setuid > setgid > sticky+ow > ow > sticky > exec),
  # while we combine applicable color codes. This is acceptable for most use cases.
  #
  # Types:
  #
  #   ? unknown
  #   p fifo
  #   c chardev
  #   d directory
  #   b blockdev
  #   - normal
  #   s socket
  #   w whiteout
  #   l symlink (good)
  #   L symlink (loop)
  #   N symlink (nonexistent)

  if (( tilde_unexpand )); then
    # This implementation is the same as below except that $file in the second output field
    # is replaced with ${(D)file}.
    for file m in ${files:^modes}; do
      local key=$file
      if (( encode_key )); then
        local -a reply=()
        -z4m-b64enc "$key" || continue
        key=$reply[1]
      fi
      if [[ ${style::=${${_z4m_mode_codes[$((m & 64075))]:-$'\0'}/%$'\0'/$'\0'$_z4m_name_colors[(ke)$file:t]}} \
              == $'@\0.' ]]; then
        if ! zstat -L -A link +link -- $file; then
          printf '%1$s\0\e[%3$sm%4$s\e[0m\n' $key $file "${or:-$_z4m_name_colors[$file:t]}" ${(D)file}
        elif ! zstat -A link_mode +mode -- $file; then
          printf '%1$s\0\e[%3$sm%6$s\e[0m -> \e[%5$sm%4$q\e[0m\n' \
            $key $file "${or:-$_z4m_name_colors[$file:t]}"             \
            $link "${or:-$_z4m_name_colors[$link:t]}"             \
            ${(D)file}
        elif (( ln_target )); then
          printf '%1$s\0\e[%4$sm%5$s\e[0m -> \e[%4$sm%2$q\e[0m%3$s\n'                                             \
            $key $link                                                                                           \
            "${(@0)${${_z4m_mode_codes[$((link_mode & 64075))]:-$nil}/%$nil/$nil$_z4m_name_colors[(ke)$link:t]}}" \
            ${(D)file}
        else
          printf '%1$s\0\e[%3$sm%6$s\e[0m -> \e[%5$sm%2$q\e[0m%4$s\n'                                             \
            $key $link "${ln:-$_z4m_name_colors[$link:t]}"                                                       \
            "${(@0)${${_z4m_mode_codes[$((link_mode & 64075))]:-$nil}/%$nil/$nil$_z4m_name_colors[(ke)$link:t]}}" \
            ${(D)file}
        fi
      else
        printf '%1$s\0\e[%4$sm%5$s\e[0m%3$s\n' $key $file "${(@0)style}" ${(D)file}
      fi
    done
  else
    # This implementation works whether _z4m_name_colors is empty or not.
    for file m in ${files:^modes}; do
      local key=$file
      if (( encode_key )); then
        local -a reply=()
        -z4m-b64enc "$key" || continue
        key=$reply[1]
      fi
      if [[ ${style::=${${_z4m_mode_codes[$((m & 64075))]:-$'\0'}/%$'\0'/$'\0'$_z4m_name_colors[(ke)$file:t]}} \
              == $'@\0.' ]]; then
        if ! zstat -L -A link +link -- $file; then
          printf '%1$s\0\e[%3$sm%2$q\e[0m\n' $key $file "${or:-$_z4m_name_colors[$file:t]}"
        elif ! zstat -A link_mode +mode -- $file; then
          printf '%1$s\0\e[%3$sm%2$q\e[0m -> \e[%5$sm%4$q\e[0m\n' \
            $key $file "${or:-$_z4m_name_colors[$file:t]}"             \
            $link "${or:-$_z4m_name_colors[$link:t]}"
        elif (( ln_target )); then
          printf '%1$s\0\e[%4$sm%2$q\e[0m -> \e[%4$sm%3$q\e[0m%5$s\n' \
            $key $file $link                                               \
            "${(@0)${${_z4m_mode_codes[$((link_mode & 64075))]:-$nil}/%$nil/$nil$_z4m_name_colors[(ke)$link:t]}}"
        else
          printf '%1$s\0\e[%3$sm%2$q\e[0m -> \e[%6$sm%4$q\e[0m%5$s\n' \
            $key $file $link "${ln:-$_z4m_name_colors[$link:t]}"           \
            "${(@0)${${_z4m_mode_codes[$((link_mode & 64075))]:-$nil}/%$nil/$nil$_z4m_name_colors[(ke)$link:t]}}"
        fi
      else
        printf '%1$s\0\e[%4$sm%2$q\e[0m%3$s\n' $key $file "${(@0)style}"
      fi
    done
  fi
done
