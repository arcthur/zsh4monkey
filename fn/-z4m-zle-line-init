#!/usr/bin/env zsh

local -i ret

if [[ ${CONTEXT-} != start || -v _z4m_transient_rprompt ]]; then
  if [[ ${CONTEXT} == start ]]; then
    unsetopt transient_rprompt
    unset _z4m_transient_rprompt
  elif [[ ! -o transient_rprompt ]]; then
    setopt transient_rprompt
    typeset -g _z4m_transient_rprompt=
  fi
fi

if (( ${+widgets[-z4m-orig-zle-line-init]} )); then
  zle -- -z4m-orig-zle-line-init "$@" || ret=$?
fi

if (( ${+_z4m_redraw_fd} )); then
  zle -F "$_z4m_redraw_fd"
  exec {_z4m_redraw_fd}>&-
  unset _z4m_redraw_fd
fi

unset POSTDISPLAY _z4m_autosuggest_buffer _z4m_autosuggestion
-z4m-highlight-reset init

if [[ ${(%):-%~} != ${_z4m_last_dir-} && ! -v _z4m_dir_hist_fd ]]; then
  typeset -gi _z4m_dir_hist_fd
  if sysopen -o cloexec -ru _z4m_dir_hist_fd /dev/null; then
    zle -F $_z4m_dir_hist_fd -z4m-update-dir-history
  else
    unset _z4m_dir_hist_fd
  fi
fi

# Vi mode: reset to insert mode and beam cursor at line start
if (( ${+_Z4M_VI_MODE_ENABLED} )); then
  -z4m-set-cursor-shape beam
  # Ensure we start in insert mode
  [[ $KEYMAP == vicmd ]] && zle -K viins
fi

return ret
