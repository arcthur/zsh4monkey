#!/usr/bin/env zsh

# Runs with user options.
#
# Precondition: [[ -e $1 ]].

local -a stat

# Checking [[ -e "$1".zwc ]] is faster than redirecting stderr of zstat to /dev/null.
[[ -e "$1".zwc ]] && zstat +mtime -A stat -- "$1" "$1".zwc && {
  # Negative indices to handle ksh_arrays.
  (( stat[-1] == stat[-2] + 1 )) && return  # common case
  stat[-1]=()
} || {
  zstat +mtime -A stat -- "$1" || return
}

[[ -w "${1:h}" ]] || return

local t
strftime -s t '%Y%m%d%H%M.%S' $((stat + 1))

local src_dir="${1:h}"
local src_base="${1:t}"
local tmp="$1".tmp."${sysparams[pid]}".zwc
local tmp_base="$src_base".tmp."${sysparams[pid]}".zwc
{
  (( !_z4m_dangerous_root ))       &&
    (
      builtin cd -q -- "$src_dir" &&
        zcompile -R -- "$tmp_base" "$src_base" &&
        command touch -ct $t -- "$tmp_base" &&
        zf_rm -f -- "$src_base".zwc &&
        zf_mv -f -- "$tmp_base" "$src_base".zwc
    )
} always {
  (( $? )) && zf_rm -f -- "$tmp" "$1".zwc 2>/dev/null
}
