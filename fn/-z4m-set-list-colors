#!/usr/bin/env zsh

emulate -L zsh -o typeset_silent -o pipe_fail -o extended_glob \
    -o prompt_percent -o no_prompt_subst -o no_prompt_bang -o no_bg_nice -o no_aliases
setopt octalzeroes

local curcontext=$1
local list_types=$2

local -a list_colors
zstyle -a :completion:$curcontext:default list-colors list_colors || return
(( $#list_colors )) || return

if [[ ${list_types}${(pj:\0:)list_colors} != $_z4m_last_list_colors ]]; then
  # This takes ~20ms. Can be easily optimized.
  typeset -g _z4m_last_list_colors=${list_types}${(pj:\0:)list_colors}
  # Filter out mode colors (two-letter codes like di=, ln=, etc.) and parse name colors
  local -a name_parts=(${(@s:=:)${(@s.:.)list_colors}:#[[:alpha:]][[:alpha:]]=*})
  # Ensure even number of elements for associative array
  (( $#name_parts % 2 == 0 )) || name_parts+=('')
  typeset -gA _z4m_name_colors=("${name_parts[@]}")
  typeset -gA _z4m_mode_colors=(${(@Ms:=:)${(@s.:.)list_colors}:#[[:alpha:]][[:alpha:]]=*})
  typeset -gA _z4m_mode_codes=()
  () {
    local -i8 a b c d
    local -a codes
    local suf
    for a in 0140000 0120000 0100000 0060000 0040000 0020000 0010000; do
      for b in 0 02000 04000 06000; do
        for c in 0 01000 00002 01002; do
          for d in 0 0001 0010 0011 0100 0101 0110 0111; do
            suf=
            codes=()
            if (( a == 0120000 )); then
              _z4m_mode_codes[$((a|b|c|d))]=$'@\0.'
              continue
            fi
            if (( a == 0100000 )); then
              (( d )) && suf='*'
            else
              if   (( a == 0140000 )); then codes+=($_z4m_mode_colors[so]); suf='=';
              elif (( a == 0060000 )); then codes+=($_z4m_mode_colors[bd]); suf='' ;
              elif (( a == 0040000 )); then codes+=($_z4m_mode_colors[di]); suf='/';
              elif (( a == 0020000 )); then codes+=($_z4m_mode_colors[cd]); suf='' ;
              elif (( a == 0010000 )); then codes+=($_z4m_mode_colors[pi]); suf='|'; fi
              if   (( c ==   01000 )); then codes+=($_z4m_mode_colors[st]);
              elif (( c ==   00002 )); then codes+=($_z4m_mode_colors[ow]);
              elif (( c ==   01002 )); then codes+=($_z4m_mode_colors[tw]); fi
            fi
            (( b & 04000 )) && codes+=($_z4m_mode_colors[su])
            (( b & 02000 )) && codes+=($_z4m_mode_colors[sg])
            (( $#codes || a != 0100000 || !d )) || codes+=($_z4m_mode_colors[ex])
            (( list_types )) || suf=
            _z4m_mode_codes[$((a|b|c|d))]=$suf$'\0'${(j:;:)codes}
          done
        done
      done
    done
  }
fi

return 0
