#!/usr/bin/env zsh

local -i ret

if (( ${+widgets[-z4m-orig-zle-line-pre-redraw]} )); then
  zle -- -z4m-orig-zle-line-pre-redraw "$@" || ret=$?
fi

if (( ${+_z4m_substring_search_highlight} )) &&
   [[ ${_history_substring_search_cursor-} != $CURSOR ||
      ${_history_substring_search_result-} != $BUFFER ]]; then
  typeset -g _history_substring_search_result=
  typeset -g _history_substring_search_query_highlight=
  unset _z4m_substring_search_highlight _history_substring_search_cursor
fi

if (( PENDING || KEYS_QUEUED_COUNT )); then
  if (( ! ${+_z4m_redraw_fd} )); then
    typeset -gi _z4m_redraw_fd
    if sysopen -o cloexec -ru _z4m_redraw_fd /dev/null; then
      zle -F $_z4m_redraw_fd -z4m-redraw-buffer
    else
      unset _z4m_redraw_fd
    fi
  fi

  if (( ${+_z4m_highlight_state} )) && (( ${_z4m_highlight_state[overlay_count]:-0} > 0 )); then
    -z4m-highlight-core-strip-overlays || true
  fi

  if [[ -n ${POSTDISPLAY-} && ${#BUFFER} -ne ${#_z4m_autosuggest_buffer} ]]; then
    POSTDISPLAY=${_z4m_autosuggestion:${#BUFFER}}
    [[ -n $POSTDISPLAY ]] || unset POSTDISPLAY _z4m_autosuggest_buffer _z4m_autosuggestion
  fi

  # Re-apply autosuggestion overlay so POSTDISPLAY keeps its dim style during fast typing
  if [[ -n ${POSTDISPLAY-} && -n ${ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE-} ]]; then
    region_highlight+=("${#BUFFER} $(( ${#BUFFER} + ${#POSTDISPLAY} )) ${ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE}")
    (( ${+_z4m_highlight_state} )) && _z4m_highlight_state[overlay_count]=$(( ${_z4m_highlight_state[overlay_count]:-0} + 1 ))
  fi
else
  -z4m-redraw-buffer
fi

return ret
